//Globals
Texture2D shaderTexture;
SamplerState SampleType;

cbuffer LightBuffer
{
	float4 ambientColor;
	float4 diffuseColor;
	float3 lightDirection;
	float specularPower;
	float4 specularColor;
};

//Typedefs
struct PixelInputType
{
	float4 position : SV_POSITION;
	float2 tex : TEXCOORD0;
	float3 normal : NORMAL;
	float3 viewDirection : TEXCOORD1;
};

//Pixel Shader
float4 LightPixelShader(PixelInputType input) : SV_TARGET
{
	float4 textureColor;
	float lightIntensity;
	float4 color;
	float3 reflection;
	float4 specular = float4(0.0f, 0.0f, 0.0f, 0.0f);
	
	//Sample the pixel color from the texure using the sampler at this texture coordinate location
	textureColor = shaderTexture.Sample(SampleType, input.tex);
	
	//Set default to ambient color
	color = ambientColor;

	//Calculate the amount of light on this pixel by obtaining the value of dot product of vertex normal and negative light direction
	lightIntensity = saturate(dot(input.normal, -lightDirection));
	
	//Determine the final diffuse color
	if (lightIntensity > 0.0f)
	{
		color += (diffuseColor * lightIntensity);

		//Saturate diffuse + ambient light color to ensure it is below 1.0f
		color = saturate(color);

		//Calculate the reflection vector. #TODO Figure out the calculation for the reflection vector
		reflection = normalize(2 * lightIntensity * input.normal + lightDirection); 

		//Determine the amount of specular light based on reflection vector, viewing direction, and specular power
		specular = pow(saturate(dot(reflection, input.viewDirection)), specularPower);
	}
	
	//Multiply the texturePixel and final diffuse color to get the final pixel color result
	color = color * textureColor;

	//We only add the specular power at the end. This is because specular is a highlight, and should be added to the penultimate value in order to show up properly
	color = saturate(color + specular);
	
	return color;
}